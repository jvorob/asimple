project('asimple', 'c',
  default_options: [
    'b_lto=true',
    'b_staticpic=false',
    'c_std=c2x',
    'warning_level=3',
  ],
  license: ['Apache-2.0'],
  version: '0.1.0')

boards = [
  { 'name': 'asimple_rba_atp', 'dependency': 'ambiq_rba_atp'},
  { 'name': 'asimple_rba', 'dependency': 'ambiq_rba'},
]

c_args = [
  '-ffunction-sections',
]

link_args = [
  '-Wl,--gc-sections', '-fno-exceptions',
]

cc = meson.get_compiler('c', native: false)
lfs_lib = cc.find_library('lfs', has_headers: ['lfs.h'], required: true)

# Libraries per board
foreach board : boards
  ambiq_lib = dependency(board['dependency'])

  lib_sources = files([
    'src/uart.c',
    'src/adc.c',
    'src/spi.c',
    'src/lora.c',
    'src/gpio.c',
    'src/am1815.c',
    'src/syscalls.c',
    'src/flash.c',
    'src/bmp280.c',
    'src/power_control.c',
    'src/pdm.c',
    'src/asimple_littlefs.c',
  ])

  includes = include_directories([
    'include/asimple',
  ])

  lib = library(board['name'],
    lib_sources,
    dependencies: [lfs_lib, ambiq_lib],
    include_directories: includes,
    c_args: c_args,
    link_args: link_args,
    install: true,
  )

  install_subdir('include', install_dir: '')

  # Create a pkgconfig file
  pkg = import('pkgconfig')
  pkg.generate(lib, subdirs: ['', 'asimple'])
endforeach

# Executables -- FIXME this is here more as an example for now...
sources = files([
  'src/main.c',
])

# Find libm...
m_dep = cc.find_library('m', required : false)

ambiq_lib = dependency('ambiq_rba_atp')
exe = executable(meson.project_name(),
  sources,
  dependencies: [ambiq_lib, m_dep],
  link_with: lib,
  include_directories: includes,
  c_args: c_args,
  link_args: link_args + ['-T' + meson.source_root() / 'linker.ld']
)

# Custom flash target, to make it easier to program the RedBoard, requires
# objcopy
objcopy = find_program('objcopy')

bin = custom_target(
  input : exe,
  output : exe.name().split('.')[0] + '.bin',
  command : [objcopy, '-O', 'binary', '@INPUT@', '@OUTPUT@', ],
  build_by_default: true
)

run_target('flash',
  command : ['python3', meson.source_root() / 'svl.py',
    get_option('tty'), '-f',  bin, '-b', '921600', '-v'],
  depends : bin,
)
